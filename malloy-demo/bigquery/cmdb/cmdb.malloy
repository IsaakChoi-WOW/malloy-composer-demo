--! styles cmdb.styles.json


source: cmdb is table('gcp-wow-ent-cyber-sechub-prod.app_cmdbassets.mv_report_cmdbassets_applications_mapped_to_capabilities'){
  primary_key: sys_id
  measure:
    count_assets is count()
    count_unique_owners is count(distinct owned_by)
    count_assets_managed is count() {where: gm_domain != null}
    count_assets_with_null_gm is count() {where: gm_domain = null}
    count_domains is count(distinct gm_domain)
    
    asset_capability_count is count()

  query: level_distribution_bar_chart is {
    group_by: actual_level
    aggregate:
      count_assets
  }

  query: organisational_domain_bar_chart is {
    group_by: organisational_domain
    aggregate:
      count_assets
  }

  query: business_application_distribution_bar_chart is {
    top: 10
    group_by: business_application_name
    aggregate:
      count_assets
  }

  query: asset_capability_distribution_bar_chart is {
    top: 15
    group_by: capability
    aggregate:
      count_assets
  }

  query: asset_count_per_owner is {
    top: 10
    order_by: count_assets desc
    group_by:
      owned_by
    aggregate:
      count_assets
  }  

  query: gm_assets_managed is {
    top: 10
    order_by: count_assets_managed desc
    group_by:
      gm_domain
    aggregate:
      count_assets_managed
  }

  query: gm_dashbaord is {
    aggregate: 
      count_domains
      count_assets
      count_assets_with_null_gm
    nest: 
      gm_assets_managed
    nest:
      by_gm_dashboard is {
        top: 10
        group_by: 
          gm_domain
        aggregate: 
          count_assets_managed
        nest: 
          organisational_domain_bar_chart
        nest:
          asset_capability_distribution_bar_chart
        nest:
          business_application_distribution_bar_chart
      }
  }
}